# quasar: local ai. quasard picks a chat model from the manifest for the tier orbit reports, runs
# llama-server (vulkan + cpu) as its child on a unix socket only quasar's user can open, serves the
# local api on 127.0.0.1 in front of it and answers on the system bus as dev.rift.Quasar. a second
# llama-server runs the embedding model for search by meaning, and the owner's own timer keeps an
# index of home with it. whisper and piper come later.
{
  config,
  lib,
  pkgs,
  self,
  ...
}:
let
  cfg = config.rift.quasar;
  busName = "dev.rift.Quasar";
  # the embedding model quasard runs for search by meaning. the index waits for its file
  embedding = builtins.head (lib.importTOML ../../models/manifest.toml).embedding;
  # the voice turns words into phonemes with espeak's data. the screen reader in basics.nix uses
  # the same build, so this is one store path, not two
  espeak = pkgs.espeak-ng.override { mbrolaSupport = false; };
  # anyone on the machine may ask and read the properties. only quasar's own user owns the name
  policy = pkgs.writeTextFile {
    name = "quasar-dbus-policy";
    destination = "/share/dbus-1/system.d/${busName}.conf";
    text = ''
      <!DOCTYPE busconfig PUBLIC "-//freedesktop//DTD D-BUS Bus Configuration 1.0//EN"
       "http://www.freedesktop.org/standards/dbus/1.0/busconfig.dtd">
      <busconfig>
        <policy user="quasar">
          <allow own="${busName}"/>
        </policy>
        <policy context="default">
          <allow send_destination="${busName}" send_interface="${busName}"/>
          <allow send_destination="${busName}" send_interface="org.freedesktop.DBus.Properties"/>
          <allow send_destination="${busName}" send_interface="org.freedesktop.DBus.Introspectable"/>
          <allow send_destination="${busName}" send_interface="org.freedesktop.DBus.Peer"/>
        </policy>
      </busconfig>
    '';
  };
in
{
  options.rift.quasar = {
    enable = lib.mkEnableOption "Quasar, the local AI service";

    daemon = lib.mkOption {
      type = lib.types.package;
      default = self.packages.${pkgs.stdenv.hostPlatform.system}.workspace;
      description = "The build that provides quasard.";
    };

    package = lib.mkOption {
      type = lib.types.package;
      default = pkgs.llama-cpp.override { vulkanSupport = true; };
      description = "llama.cpp build used for inference. Vulkan so it works on any GPU vendor.";
    };

    voice = lib.mkOption {
      type = lib.types.package;
      default = pkgs.sherpa-onnx;
      description = "The program that says words out loud. It reads the piper voice in the manifest.";
    };

    modelsDir = lib.mkOption {
      type = lib.types.str;
      default = "/var/lib/rift/models";
      description = "Where the GGUF weights live (the @models subvolume on persist).";
    };

    model = lib.mkOption {
      type = lib.types.nullOr lib.types.str;
      default = null;
      description = "A chat model id or file from the manifest to run instead of the one the tier picks.";
    };

    port = lib.mkOption {
      type = lib.types.port;
      default = 11434;
      description = "Localhost port for the OpenAI-compatible API. quasard serves it and refuses requests from web pages.";
    };

    contextSize = lib.mkOption {
      type = lib.types.int;
      default = 8192;
    };
  };

  config = lib.mkIf cfg.enable {
    environment.systemPackages = [ cfg.package ];
    # the one list of models. quasard reads it here
    environment.etc."rift/models.toml".source = ../../models/manifest.toml;
    services.dbus.packages = [ policy ];

    users.users.quasar = {
      isSystemUser = true;
      group = "quasar";
      description = "Quasar";
    };
    users.groups.quasar = { };

    systemd.services.quasar = {
      description = "Quasar, the local AI service";
      wantedBy = [ "multi-user.target" ];
      requires = [ "dbus.service" ];
      # orbit counts as started once its name is on the bus, and by then it knows the tier
      after = [
        "dbus.service"
        "orbit.service"
      ];
      unitConfig.RequiresMountsFor = [ cfg.modelsDir ];
      serviceConfig = {
        # quasard takes the name once it has the tier. the model loads after that, the State
        # property says when it is ready
        Type = "dbus";
        BusName = busName;
        ExecStart = lib.concatStringsSep " " (
          [
            "${cfg.daemon}/bin/quasard"
            "--manifest /etc/rift/models.toml"
            "--models-dir ${cfg.modelsDir}"
            "--llama-server ${cfg.package}/bin/llama-server"
            "--port ${toString cfg.port}"
            "--socket /run/quasar/llama.sock"
            "--embedding-socket /run/quasar/embed.sock"
            "--ctx-size ${toString cfg.contextSize}"
            "--voice ${cfg.voice}/bin/sherpa-onnx-offline-tts"
            "--voice-data ${espeak}/share/espeak-ng-data"
          ]
          ++ lib.optional (cfg.model != null) "--model ${cfg.model}"
        );
        Restart = "on-failure";
        # llama-server's socket. nobody else may open it, the local api is the way in
        RuntimeDirectory = "quasar";
        RuntimeDirectoryMode = "0700";
        User = "quasar";
        Group = "quasar";
        # llama-server is quasard's child, everything below holds for it too
        SupplementaryGroups = [
          "render"
          "video"
        ];
        DeviceAllow = [ "char-drm rw" ];
        ReadOnlyPaths = [ cfg.modelsDir ];
        # quasar never talks to the network, localhost only. the bus is a unix socket
        IPAddressDeny = "any";
        IPAddressAllow = [ "localhost" ];
        PrivateTmp = true;
        ProtectSystem = "strict";
        ProtectHome = true;
        NoNewPrivileges = true;
      };
    };

    # search by meaning. the owner's user manager keeps the index of home in the owner's cache:
    # rift ai index reads the files and quasar only turns their text into vectors. the first run
    # waits until the session has settled, then one runs 15 minutes after the last ended
    systemd.user.services.quasar-index = {
      description = "Quasar, the search index of home";
      unitConfig.ConditionPathExists = "${cfg.modelsDir}/${embedding.file}";
      # airlock and bwrap for the pdfs in home, whose text is written out in a sandbox, and
      # pdftotext, which is what runs in it
      path = [
        cfg.daemon
        pkgs.bubblewrap
        pkgs.poppler-utils
      ];
      serviceConfig = {
        Type = "oneshot";
        ExecStart = "${cfg.daemon}/bin/rift ai index";
        Nice = 19;
        IOSchedulingClass = "idle";
      };
    };
    systemd.user.timers.quasar-index = {
      description = "Quasar, the search index of home, every 15 minutes";
      wantedBy = [ "timers.target" ];
      timerConfig = {
        OnStartupSec = "10min";
        OnUnitInactiveSec = "15min";
      };
    };
  };
}
